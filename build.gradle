group 'edu.sfsu.worldofbalance.atnsimulator'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = "edu.sfsu.worldofbalance.atnsimulator.BatchSimulator"

repositories {
    mavenCentral()

    // Fake Ivy repository for jars not available in any standard repositories
    // (i.e. JHDF5)
    ivy {
        url "https://brsaylor.github.io/jars"
        layout 'pattern' , {
            ivy '[module]-ivy.xml'
            artifact '[module]-[revision](.[ext])'
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile group: 'com.beust', name: 'jcommander', version: '1.64'

    compile group: 'ch.systemsx.cisd', name: 'sis-jhdf5-batteries_included', version: '14.12.6'
    // If you any trouble with Gradle not downloading this dependency automatically,
    // download the zip file from https://wiki-bsse.ethz.ch/display/JHDF5/Download+Page
    // find within it the jar file named below,
    // place it in a directory called libs,
    // and uncomment the following line.
    // compile files('libs/sis-jhdf5-batteries_included.jar')

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

run {
    // Allow passing arguments to the program from the command line with:
    // gradle run -Pargs="-my args for the system"
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}